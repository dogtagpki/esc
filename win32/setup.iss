; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Smart Card Manager
AppMutex=ESCMutex
AppVerName=Smart Card Manager 1.1.0-12
AppPublisher=Fedora
CreateAppDir=true
Compression=lzma
SolidCompression=true
MinVersion=0,5.0.2195
ShowLanguageDialog=yes
OutputBaseFilename=SmartCardManagerSetup-1.1.0-12.win32.i386
DefaultDirName={pf}\Fedora\ESC
DisableProgramGroupPage=false
DefaultGroupName=Fedora
SetupIconFile=BUILD\ESC\components\esc.ico
UninstallDisplayIcon={app}\components\esc.ico
WizardImageFile=BUILD\ESC\chrome\content\esc\esc-image-large.bmp
WizardSmallImageFile=BUILD\ESC\components\esc.bmp
AllowNoIcons=yes
LicenseFile=esc-license.txt
InfoBeforeFile=info-before.txt
InfoAfterFile=info-after.txt
PrivilegesRequired=admin
VersionInfoVersion=1.1.0.12


[Files]
Source: BUILD\clkcsp.dll; DestDir: {sys}; Flags: regserver restartreplace
Source: BUILD\cspres.dll; DestDir: {sys}; Flags: restartreplace
Source: BUILD\pk11install.exe; DestDir: {app}\PKCS11

Source: BUILD\libnspr4.dll; DestDir: {app}\PKCS11
Source: BUILD\libplc4.dll; DestDir: {app}\PKCS11
Source: BUILD\libplds4.dll; DestDir: {app}\PKCS11
Source: BUILD\softokn3.dll; DestDir: {app}\PKCS11
Source: BUILD\nssutil3.dll; DestDir: {app}\PKCS11
Source: BUILD\sqlite3.dll;  DestDir: {app}\PKCS11


Source: BUILD\clkcsp.sig; DestDir: {sys}

Source: BUILD\atl71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist

Source: BUILD\msvcr71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist

Source: BUILD\msvcp71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist

Source: BUILD\mfc71.dll; DestDir: {sys}; Flags: uninsneveruninstall onlyifdoesntexist

; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;Source: BUILD\ESC\components\rhTray.xpt; DestDir: {app}\components
;Source: BUILD\ESC\components\rhCoolKey.xpt; DestDir: {app}\components
Source: BUILD\ESC\components\rhICoolKey.xpt; DestDir: {app}\components
Source: BUILD\ESC\components\rhIKeyNotify.xpt; DestDir: {app}\components
Source: BUILD\ESC\components\rhITray.xpt; DestDir: {app}\components
Source: BUILD\ESC\components\rhITrayWindNotify.xpt; DestDir: {app}\components
Source: BUILD\ESC\components\rhTray.dll; DestDir: {app}\components
Source: BUILD\ESC\components\esc.ico; DestDir: {app}\components
Source: BUILD\ESC\components\rhCoolKey.dll; DestDir: {app}\components
Source: BUILD\coolkeypk11.dll; DestDir: {sys}; Flags: restartreplace
Source: BUILD\libckyapplet-1.dll; DestDir: {sys}; Flags: restartreplace
Source: BUILD\zlib1.dll; DestDir: {sys}; Flags: restartreplace
;Source: BUILD\coolkeypk11.dll; DestDir: {app}\PKCS11
Source: BUILD\ESC\esc.exe; DestDir: {app}
;Source: BUILD\ESC\esc.bat; Destdir: {app}
Source: BUILD\ESC\application.ini; DestDir: {app}
Source: BUILD\ESC\chrome\chrome.manifest; DestDir: {app}\chrome
Source: BUILD\ESC\chrome\content\esc\TRAY.js; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\certinfo.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\config.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\contents.rdf; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\EnrollPopupInclude.html; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\esc.css; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\ESC.js; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\esc.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\security.xul; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\esc_browser.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\GenericAuth.js; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\GenericAuth.xul; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\GenericAuthInclude.html; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\logo.gif; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\settings.xul; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\style.css; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\bg.jpg; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\icons\default\esc-window.ico; DestDir: {app}\chrome\icons\default
Source: BUILD\ESC\chrome\icons\default\settings-window.ico; DestDir: {app}\chrome\icons\default
Source: BUILD\ESC\chrome\locale\en-US\esc.properties; DestDir: {app}\chrome\locale\en-US
Source: BUILD\ESC\chrome\locale\en-US\esc.dtd; DestDir: {app}\chrome\locale\en-US
Source: BUILD\ESC\chrome\content\esc\throbber-anim5.gif; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\2-vweak.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\3-weak.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\4-fair.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\5-good.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\6-strong.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\blank-card.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\enrolled-key.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\initializecard.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\progress.7.gif; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\1-none.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\components\escCLH.js; DestDir: {app}\components
;Source: BUILD\ESC\chrome\content\esc\hiddenWindow.html; DestDir: {app}\chrome\content\esc
;Source: BUILD\ESC\chrome\content\esc\aol.gif; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\AdvancedInfo.js; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\advancedinfo.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\blank-cardx2.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\password.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\enroll.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\enrolled-keyx2.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\enrollx2.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\esc-client-24.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\formatcard.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\formatcardx2.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\hiddenWindow.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\initializecardx2.png; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\MineOverlay.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\password.js; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\certManager.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\chrome\content\esc\getcachain.xul; DestDir: {app}\chrome\content\esc
Source: BUILD\ESC\defaults\preferences\esc-prefs.js; DestDir: {app}\defaults\preferences
; Xulrunner

Source: BUILD\ESC\xulrunner\*; DestDir: {app}\xulrunner; Flags: ignoreversion recursesubdirs createallsubdirs

[Run]

Filename: {app}\PKCS11\pk11install.exe; Parameters: "-v ""name='CoolKey Module' library=coolkeypk11.dll NSS=""slotParams={{0x1=[slotFlags=PublicCerts]}"""""; WorkingDir: {sys}; StatusMsg: Configuring System for smart cards...
Filename: {app}\xulrunner\xulrunner.exe; Parameters: """--register-global"""
Filename: {app}\esc.exe; WorkingDir: {app}; StatusMsg: {code:GetEscStatusMsg}; Flags: nowait

[UninstallRun]
Filename: {app}\xulrunner\xulrunner.exe; Parameters: """--unregister-global"""
[UninstallDelete]
Type: filesandordirs; Name: {app}
[_ISTool]
OutputExeFilename=BUILD\coolkey-setup.exe
UseAbsolutePaths=false
LogFile=inst.log
LogFileAppend=false
[Registry]
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Defaults\Provider\CoolKey PKCS #11 CSP; ValueType: string; ValueName: PKCS11Module; ValueData: coolkeypk11.dll; Flags: uninsdeletekey
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Axalto Developer; ValueType: binary; ValueName: ATRMask; ValueData: ff ff ff ff ff ff ff ff 00 00; Flags: uninsdeletekey
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Axalto Developer; ValueType: string; ValueName: Crypto Provider; ValueData: CoolKey PKCS #11 CSP
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Axalto Developer; ValueType: binary; ValueName: ATR; ValueData: 3b 75 94 00 00 62 02 02 00 00

; Now register the Gemalto 64K V2
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Gemalto 64K V2; ValueType: binary; ValueName: ATRMask; ValueData: ff ff 00 ff 00 ff ff ff 00 00; Flags: uninsdeletekey

Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Gemalto 64K V2; ValueType: string; ValueName: Crypto Provider; ValueData: CoolKey PKCS #11 CSP

Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Gemalto 64K V2; ValueType: binary; ValueName: ATR; ValueData: 3b 95 00 40 00 ae 01 03 00 00

; Now register the Safenet 330J 
Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Safenet 330J; ValueType: binary; ValueName: ATRMask; ValueData: ff ff ff ff ff ff ff ff ff ff ff ff ff ff ff ff ff ff ff 00 00; Flags: uninsdeletekey

Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Safenet 330J; ValueType: string; ValueName: Crypto Provider; ValueData: CoolKey PKCS #11 CSP

Root: HKLM; Subkey: Software\Microsoft\Cryptography\Calais\SmartCards\Safenet 330J; ValueType: binary; ValueName: ATR; ValueData: 3b ec 00 ff 81 31 fe 45 a0 00 00 00 56 33 33 30 4a 33 06 00 00


Root: HKLM; Subkey: Software\
; Turn off the "pick a cert" dialog box
Root: HKCU; Subkey: Software\Microsoft\Windows\CurrentVersion\Internet Settings\Zones\3; ValueType: dword; ValueName: 1A04; ValueData: 0
; Enable TLS 1.0
Root: HKCU; Subkey: Software\Microsoft\Windows\CurrentVersion\Internet Settings; ValueType: dword; ValueName: SecureProtocols; ValueData: 168
[Icons]
Name: {userdesktop}\Smart Card Manager; Filename: {app}\esc.exe; WorkingDir: {app}; IconFileName : {app}\components\esc.ico
Name: {group}\ESC\Smart Card Manager; Filename: {app}\esc.exe; WorkingDir: {app}; IconFileName : {app}\components\esc.ico
Name: {commonstartup}\Smart Card Manager; Filename: {app}\esc.exe; WorkingDir: {app}; IconFileName : {app}\components\esc.ico

[Dirs]
Name: {app}\PKCS11
Name: {app}\components; Flags: deleteafterinstall uninsalwaysuninstall
Name: {app}\chrome
Name: {app}\defaults
Name: {app}\xulrunner
Name: {app}\chrome\content
Name: {app}\chrome\icons
Name: {app}\chrome\content\esc
Name: {app}\chrome\locale
Name: {app}\chrome\locale\en-US
Name: {app}\chrome\icons\default
Name: {app}\xulrunner\chrome
Name: {app}\xulrunner\components
Name: {app}\xulrunner\defaults
Name: {app}\xulrunner\greprefs
Name: {app}\xulrunner\plugins
Name: {app}\xulrunner\res
Name: {app}\xulrunner\sdk
Name: {app}\xulrunner\defaults\autoconfig
Name: {app}\xulrunner\defaults\pref
Name: {app}\xulrunner\defaults\profile
Name: {app}\xulrunner\defaults\profile\chrome
Name: {app}\xulrunner\defaults\profile\extensions
Name: {app}\xulrunner\defaults\profile\US
Name: {app}\xulrunner\defaults\profile\US\chrome
Name: {app}\xulrunner\res\dtd
Name: {app}\xulrunner\res\entityTables
Name: {app}\xulrunner\res\fonts
Name: {app}\xulrunner\res\html
Name: {app}\xulrunner\sdk\lib
Name: {app}\xulrunner\estensions
Name: {app}\xulrunner\updates
Name: {app}\xulrunner\updates\0
Name: {app}\defaults\preferences
Name: {app}\dictionaries
Name: {app}\modules; Flags: uninsalwaysuninstall

[Code]
var
	ConfigValues:	TArrayOfString;
//Program to update the esc-prefs.js file if the program
// is given optional arguments of the form:
// /EscConfig=name=value, EX:
// /EscConfig=esc.global.phone.home.url=http://test.host.com:7888/cgi-bin/home/index.cgi

// Program either replaces an existing entry or add new entries to the end.
// Commented values are ignored.

// Read the esc-prefs.js into the program.
function ReadEscConfigFile (FileName: String): Boolean;
begin
	Result := LoadStringsFromFile (FileName, ConfigValues);
end;

// Return TRUE if the pref already is in the file.
function QueryEscConfigValue ( Name: String ;var  Index: LongInt): Boolean;
var
	I:		LongInt;
	ValuePos: Integer;
	CommentPos: Integer;
begin

	For I := 0 to GetArrayLength (ConfigValues) - 1 do
	begin

    ValuePos :=  Pos ( Name, ConfigValues[I]);
    CommentPos := Pos('#',ConfigValues[I]);

		if ((CommentPos = 0) and (ValuePos  <>  0 )) then
		begin
			Index := I;
			Result := TRUE;
			Exit;
		end;
	end;
	Result := FALSE;
end;

//Write out the value to the config file.
// If the value exists replace it.
// If the value does not exist, add it to the end of the file.
function WriteEscConfigValue (ValueName: String; Value: String): Boolean;
var
	Index:		LongInt;
	Length:		LongInt;
	S:		    String;
	Found:    Boolean;
begin
   Length := GetArrayLength(ConfigValues);
   S := 'pref("' + Valuename + '","' + Value + '");' ;
   //MsgBox(' In WriteEscConfigValue ' + S,mbConfirmation, MB_OK);
   Found := QueryEscConfigValue (ValueName , Index);
   if( Found = TRUE) then
	 begin
	   ConfigValues [Index] :=  S;
	   Result := TRUE;
	   Exit;
	 end;
	 SetArrayLength (ConfigValues, Length + 1);
	 ConfigValues [Length] := S;
	 Result := TRUE;
end;

//Write the new esc-prefs config file with any new changes.
function WriteEscConfigFile (FileName: String): Boolean;
var
	ret:	Boolean;
begin
	
	ret := FileCopy (FileName,
		ExpandConstant ('{tmp}\' + ExtractFileName (FileName)),
		FALSE);
		
	if (ret) then
	begin
		ret := SaveStringsToFile (FileName, ConfigValues, FALSE);
		if (ret) then
		begin
			Result := TRUE;
			Exit;
		end else
		begin
			ret := FileCopy (
		     ExpandConstant ('{tmp}\' + ExtractFileName (FileName)), FileName,
		  FALSE);
		end;
	end;
	Result := FALSE;
end;

//Inno install callback, only do out stuff
// at the end of the installation procedure to process
// the esc-prefs.js file.

procedure CurStepChanged (CurStep: TSetupStep);
var
	
  NumParams: Integer;
  i: Integer;
  ConfigPos: Integer;
  ConfigStr: String;
  ConfigSuccess: Boolean;
  ConfigNeeded: Boolean;
  ConfigName:  String;
  ConfigValue: String;
begin
  ConfigNeeded  := FALSE;
  ConfigSuccess := TRUE;
	if (CurStep = ssPostInstall) then
	begin
	   ConfigSuccess := ReadEscConfigFile (ExpandConstant('{app}\defaults\preferences\esc-prefs.js'));
	   if ConfigSuccess <> TRUE then
	   begin
	      Exit;
	   end;
	  NumParams := ParamCount;
	  for i := 0 to NumParams  do
	  begin
	     ConfigPos := Pos('EscConfig=',ParamStr(i));
	
	     if( ConfigPos > 0) then
	     begin
	       ConfigStr := Copy(ParamStr(i),ConfigPos + 10, Length(ParamStr(i)));
	       ConfigPos := Pos('=',ConfigStr);
	       if ConfigPos > 0 then
	       begin
	          ConfigName := Copy(ConfigStr, 0, ConfigPos -1);
	          ConfigValue := Copy(ConfigStr, ConfigPos + 1, Length(ConfigStr));
	          if ( (Length(ConfigName) > 0) and (Length(ConfigValue) > 0 ) ) then
	          begin
	             ConfigNeeded := TRUE;
	             ConfigSuccess :=  WriteEscConfigValue(ConfigName, ConfigValue);
	          end
            else
            begin
	             ConfigSuccess := FALSE;
	          end;
	       end;
	     end;
	  end;
		
		if ConfigNeeded and ConfigSuccess then
		begin
		   WriteEscConfigFile (ExpandConstant('{app}\defaults\preferences\esc-prefs.js'));
		end;
	end;
end;

function GetEscStatusMsg(Param: String) :string;
var MyMillis: LongInt;
begin

MyMillis := 5000;

Sleep(MyMillis);

Result := 'Starting Smart Card Manager.....';
end;
